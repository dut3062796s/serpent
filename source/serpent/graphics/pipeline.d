/*
 * This file is part of serpent.
 *
 * Copyright Â© 2019-2020 Lispy Snake, Ltd.
 *
 * This software is provided 'as-is', without any express or implied
 * warranty. In no event will the authors be held liable for any damages
 * arising from the use of this software.
 *
 * Permission is granted to anyone to use this software for any purpose,
 * including commercial applications, and to alter it and redistribute it
 * freely, subject to the following restrictions:
 *
 * 1. The origin of this software must not be misrepresented; you must not
 *    claim that you wrote the original software. If you use this software
 *    in a product, an acknowledgment in the product documentation would be
 *    appreciated but is not required.
 * 2. Altered source versions must be plainly marked as such, and must not be
 *    misrepresented as being the original software.
 * 3. This notice may not be removed or altered from any source distribution.
 */

module serpent.graphics.pipeline;

public import serpent.core.context;
public import serpent.core.policy;
public import serpent.core.view;
public import serpent.graphics.display;
public import serpent.graphics.renderer;

import bindbc.bgfx;
import serpent.graphics.frame;
import std.exception : enforce;

import serpent.graphics.batch;

/**
 * The Pipeline is responsible for managing the underlying graphical context,
 * such as OpenGL (or through an abstraction like bgfx) and actually getting
 * entities on screen.
 *
 * It will precompute visible entities from the global entity cache and then
 * sort them prior to rendering.
 *
 * All rendering is done via Renderer implementations.
 */
final class Pipeline
{
private:

    Context _context = null;
    Display _display = null;
    __gshared FramePacket packet;
    Renderer[] _renderers;

    /* Temporary: We need a draw operation queue we can sort! */
    QuadBatch qb;

package:

    this(Context context, Display display)
    {
        this._display = display;
        this._context = context;

        /* Allow tuning this in future */
        packet = FramePacket(30_000);
    }

private:

    /**
     * Perform any pre-rendering we need to do, such as clearing the
     * display.
     *
     * TODO: Render everything to one framebuffer by default, and scale that framenbuffer
     * so that the QuadBatch doesn't know about scale factors. It will also help us to
     * solve the glitchy black bars when using non-aspect ratios.
     */
    final void prerender() @system @nogc nothrow
    {
        /* Set clearing of view0 background. */
        clear(0);

        /* Set up auto scaling: http://www.david-amador.com/2013/04/opengl-2d-independent-resolution-rendering/ */
        auto aspectRatio = cast(float) display.logicalWidth / cast(float) display.logicalHeight;
        int w = display.width;
        int h = cast(int)(w / aspectRatio + 0.5f);

        /* Letter box it */
        if (h > display.height)
        {
            h = display.height;
            w = cast(int)(h * aspectRatio + 0.5f);
        }

        int vpX = (display.width / 2) - (w / 2);
        int vpY = (display.height / 2) - (h / 2);

        bgfx_set_view_rect(0, cast(ushort) vpX, cast(ushort) vpY, cast(ushort) w, cast(ushort) h);

        /* Make sure view0 is drawn. */
        bgfx_touch(0);

        auto camera = display.scene.camera;
        if (camera !is null)
        {
            camera.apply();
        }
    }

    /**
     * Perform any required rendering
     */
    final void postrender() @system @nogc nothrow
    {
        /* Skip frame now */
        bgfx_frame(false);
    }

public:

    final void addRenderer(Renderer r) @safe
    {
        enforce(!context.running, "Cannot add renderers to a running context");
        _renderers ~= r;
    }
    /**
     * Clear the view
     */
    final void clear(ushort viewIndex = 0) @system @nogc nothrow
    {
        bgfx_set_view_clear(viewIndex, BGFX_CLEAR_COLOR | BGFX_CLEAR_DEPTH,
                display.backgroundColor, 1.0f, 0);
    }

    /**
     * Perform one full render tick
     */
    final void render(View!ReadOnly queryView) @system
    {
        packet.startTick();
        prerender();

        /* Query visibles */
        foreach (r; _renderers)
        {
            r.queryVisibles(queryView, packet);
        }

        /* Submission (TODO: Sort by z-index) */
        foreach (s; packet.visibleEntities)
        {
            s.renderer.submit(queryView, packet, s.id);
        }

        postrender();
    }

    /**
     * Return the underlying context
     */
    final @property Context context() @safe @nogc nothrow
    {
        return _context;
    }

    /**
     * Return the underlying display
     */
    final @property Display display() @safe @nogc nothrow
    {
        return _display;
    }

    final void bootstrap()
    {
        qb = new QuadBatch(context);
    }

    final void shutdown()
    {
        qb.destroy();
        qb = null;
    }
}
